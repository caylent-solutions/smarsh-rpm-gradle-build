// ============================================================================
// RPM Build Conventions
// ============================================================================
// Provides: Java compilation, Spring Boot plugin, dependency management,
// and repository definitions.
//
// Configuration is read from .rpmenv via the _rpmProp closure:
//   JAVA_VERSION     - Java source/target version (required)
//   ARTIFACTORY_URL  - Optional private Maven repository URL
// ============================================================================

def PKG_DIR = project.ext.get('_rpmCurrentPkgDir')
def rpmProp = project.ext.get('_rpmProp')

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

// Java version from .rpmenv (required â€” no default)
def javaVersionStr = rpmProp('JAVA_VERSION')
if (!javaVersionStr) {
    throw new GradleException(
        "JAVA_VERSION is not set. Add JAVA_VERSION=<version> to your project's .rpmenv file (e.g., JAVA_VERSION=17)."
    )
}
def javaVersion = JavaVersion.toVersion(javaVersionStr)
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

// Repository definitions
repositories {
    mavenCentral()
    mavenLocal()
    // Optional private repository - set ARTIFACTORY_URL in .rpmenv
    def artifactoryUrl = rpmProp('ARTIFACTORY_URL')
    if (artifactoryUrl) {
        maven {
            name 'artifactory'
            url artifactoryUrl
        }
    }
}

// Framework-level dependency versions (centralized for consistency across projects)
// Projects can override any version in their own ext {} block.
ext {
    set('springCloudVersion', "2023.0.3")
    set('jacksonVersion', "2.17.2")
    testcontainersVersion = '1.19.8'
}

// Dependency management BOMs (project-specific exclusions belong in build.gradle)
dependencyManagement {
    dependencies {
        dependency 'com.squareup.okhttp3:okhttp:4.10.0'
        dependency 'co.elastic.clients:elasticsearch-java:7.17.3'
        dependency 'org.elasticsearch.client:elasticsearch-rest-client:7.17.3'
    }
    imports {
        mavenBom "com.fasterxml.jackson:jackson-bom:${jacksonVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:2.4.1"
    }
}

